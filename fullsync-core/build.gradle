
apply plugin: 'java-library'

dependencies {
	api project(':fullsync-logger')
	api project(':fullsync-utils')
	implementation libraries.apacheCommonsCli
	implementation libraries.apacheCommonsNet
	api(libraries.apacheCommonsVfs) {
		exclude group: "commons-logging"
		exclude group: "org.apache.maven.scm"
		exclude group: "org.apache.hadoop"
	}
	implementation libraries.orgSambaJcifs
	implementation libraries.comJcraftJsch
	implementation libraries.orgSlf4jJclOverSlf4j
	api libraries.guice
	api libraries.guiceAssistedInject
	implementation files("lib/commons-vfs2-sandbox.jar")
	testImplementation libraries.junitJupiter
	testImplementation libraries.hamcrest
	testImplementation libraries.testcontainers
	testImplementation libraries.testcontainersJUnit
}

test {
	useJUnitPlatform {
		includeEngines 'junit-jupiter'
	}
}

jar {
	manifest {
		attributes 'Main-Class': 'net.sourceforge.fullsync.cli.Main'
		def resolvedRuntime = configurations.runtimeClasspath.resolvedConfiguration
		def jarFiles = resolvedRuntime.resolvedArtifacts.collect { a ->
			rootProject.mapArtifactToFilename a
		}
		(resolvedRuntime.files - resolvedRuntime.resolvedArtifacts*.file).collect { f ->
			jarFiles.add f.name
		}
		attributes 'Class-Path': jarFiles.join(' ')
	}
}
