plugins {
	id "java-library"
}

dependencies {
	implementation project(':fullsync-logger')
	implementation project(':fullsync-utils')
	implementation(libs.commons.cli)
	implementation(libs.commons.net)
	implementation(libs.commons.vfs) {
		exclude group: "commons-logging"
		exclude group: "org.apache.maven.scm"
		exclude group: "org.apache.hadoop"
	}
	implementation(libs.jcifs)
	implementation(libs.jsch)
	implementation(libs.jcl.over.slf4j)
	implementation(libs.guice)
	implementation(libs.guice.assistedInject)
	implementation files("lib/commons-vfs2-sandbox.jar")
	testImplementation(libs.junit.jupiter)
	testImplementation(libs.hamcrest)
	testImplementation(libs.testcontainers)
	testImplementation(libs.testcontainers.JUnit)
}

test {
	useJUnitPlatform {
		includeEngines 'junit-jupiter'
	}
}

jar {
	manifest {
		attributes 'Main-Class': 'net.sourceforge.fullsync.cli.Main'
		def resolvedRuntime = configurations.runtimeClasspath.resolvedConfiguration
		def jarFiles = resolvedRuntime.resolvedArtifacts.collect { a ->
			rootProject.mapArtifactToFilename a
		}
		(resolvedRuntime.files - resolvedRuntime.resolvedArtifacts*.file).collect { f ->
			jarFiles.add f.name
		}
		attributes 'Class-Path': jarFiles.join(' ')
	}
}
