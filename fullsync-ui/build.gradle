plugins {
	id "java-library"
}

def addSWTDependency(configuration, osName) {
	def os = ''
	if (osName.contains('windows')) {
		os = 'win32.win32'
	}
	else if (osName.contains('linux')) {
		os = 'gtk.linux'
	}
	else if (osName.contains('mac')) {
		os = 'cocoa.macosx'
	}
	project.dependencies.add(configuration, "org.eclipse.platform:org.eclipse.swt.${os}.x86_64:${libs.versions.swt.get()}") {
		exclude group: 'org.eclipse.platform', module: 'org.eclipse.swt'
	}
}

def addLocalSWTVersion(configuration) {
	return addSWTDependency(configuration, System.properties['os.name'].toLowerCase())
}

configurations {
	dist.extendsFrom runtime
}

dependencies {
	implementation project(':fullsync-core')
	implementation project(':fullsync-utils')
	implementation project(':fullsync-assets')
	implementation(libs.guice)
	implementation(libs.guice.assistedInject)
	implementation(libs.jcl.over.slf4j)
	implementation(libs.commons.vfs) {
		exclude group: "commons-logging"
		exclude group: "org.apache.maven.scm"
		exclude group: "org.apache.hadoop"
	}
	dist project(':fullsync-ui')
}

addSWTDependency "dist", "linux"
addSWTDependency "dist", "mac"
addSWTDependency "dist", "windows"
addLocalSWTVersion "implementation"

task run(type: JavaExec) {
	main = 'net.sourceforge.fullsync.ui.GuiMain'
	classpath = configurations.runtime + sourceSets.main.output
}
